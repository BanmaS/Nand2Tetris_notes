// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // Put your code here:
    //通过地址的14位和13位判断输出是RAM16K的输出、screen的输出或者是键盘的输出
    //address[14]=0,一定为RAM输出
    //address[14]=1, address[13]=0，为SCREEN输出
    //address[14]=1, address[13]=1，为KEYBOARD输出
    Not(in=address[14], out=isRAM);
    And(a=isRAM, b=load, out=isRAMload);
    RAM16K(in=in, load=isRAMload, address=address[0..13], out=RAMout);
    Not(in=address[13], out=not13);
    And(a=address[14], b=not13, out=isSCREEN);
    And(a=isSCREEN, b=load, out=isSCREENload);
    Screen(in=in, load=isSCREENload, address=address[0..12], out=SCREENout);
    Keyboard(out=KEYBOARDout);
    Mux16(a=RAMout, b=SCREENout, sel=address[14], out=tempout);
    And(a = address[14], b = address[13], out = sel2);
    Mux16(a = tempout, b = KEYBOARDout, sel = sel2, out = out);
}